plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.leonardobishop'
version = '1.1'

processResources {
    duplicatesStrategy = 'include'
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        include 'config.yml'
        expand('version': project.version)
    }
}

repositories {
    mavenCentral()

    // Spigot
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    // bStats
    maven { url = uri('https://repo.codemc.org/repository/maven-public') }
}

dependencies {
    // Spigot
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    // bStats
    implementation 'org.bstats:bstats-bukkit-lite:1.8'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
}

shadowJar {
    relocate 'org.bstats', 'com.leonardobishop.foodexpiration.libs.bstats'
    archiveClassifier.set('')
}

jar.enabled = false

tasks.build {
    dependsOn(tasks.shadowJar)
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}
